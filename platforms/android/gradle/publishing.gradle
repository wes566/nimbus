import groovy.json.JsonSlurper

apply plugin: 'com.jfrog.bintray'

// Android projects need enhanced maven plugin
if (project.getPlugins().hasPlugin('com.android.application') ||
        project.getPlugins().hasPlugin('com.android.library')) {
    apply plugin: 'com.github.dcendents.android-maven'
} else {
    apply plugin: 'maven'
}


version = VERSION
group = GROUP

install {
    repositories.mavenInstaller {
        pom {
            project {
                name project.name
                url POM_URL

                packaging POM_PACKAGING
                version version

                licenses {
                    license {
                        name POM_LICENSE_NAME
                        url POM_LICENSE_URL
                    }
                }

                developers {
                    developer {
                        name POM_DEVELOPER
                    }
                }

                scm {
                    url POM_SCM_URL
                    connection POM_SCM_CONNECTION
                    developerConnection POM_SCM_DEV_CONNECTION
                }
            }
        }
    }
}


bintray {
    def versionFile = file('../../../lerna.json')
    def parsedVersion = new JsonSlurper().parseText(versionFile.text)
    def versionToPublish = parsedVersion.version.toString()
    project.version = versionToPublish
    user = project.findProperty('bintrayUser') ?: System.getenv('BINTRAY_USER')
    key = project.findProperty('bintrayApiKey') ?: System.getenv('BINTRAY_API_KEY')
    configurations = ['archives']
    publish = true
    dryRun = false
    pkg {
        name = project.name
        repo = BINTRAY_REPO
        userOrg = BINTRAY_ORG
        licenses = [POM_LICENSE_NAME]
        vcsUrl = POM_SCM_URL
        labels = ['android', 'salesforce', 'hybrid']
        version {
            name = versionToPublish
            released = new Date()
        }
    }
}
